/*
VALLAKKOTTAI MURUGAN THUNAI
Easy Simple Trading Solutions
Telegram @easysimpletradeupdates
Website https://easysimpletrade.blogspot.com
Youtube https://www.youtube.com/@easysimpletrade
GitHub https://github.com/EasySimpleTrade
*/

RequestTimedRefresh(1,False);

function SYMEXTR(iDatafeed,iName,iDate)
{
	//Amifeed
	if(iDatafeed == "Amifeed")
		{
		result = StrReplace(iName,"WK","");
		}	
	else 	
	//Valvenet VBiz BestRT
	if(iDatafeed == "VBiz")
		{
		iDATEDMY = DateTimeFormat("%d",iDate) + StrToUpper(DateTimeFormat("%b",iDate)) + DateTimeFormat("%Y",iDate);
		result = StrReplace(StrReplace(StrReplace(iName,"_",""),"-",""),iDATEDMY,"");
		}
	else
	//Equalsolutions
	if(iDatafeed == "EqualSolutions")
		{
		iDATEYM = DateTimeFormat("%y",iDate)+StrToUpper( DateTimeFormat("%b",iDate));
		iDATED = DateTimeFormat("%d",iDate);
		result = StrReplace(StrReplace(StrReplace(iName,"_",""),iDATEYM,""),iDATED,"");
		}
	else
		
	//Default
		{
		result = iName;
		}
    return result;    
}

//Strategy Code Start---------------------------------
Buy = Sell = 0;
//Strategy Code End -----------------------------------------


_N(Title = StrFormat("{{NAME}} - {{INTERVAL}} {{DATE}} O %g, H %g, L %g, C %g, V %g  {{VALUES}}", O, H, L, C, V  ));
SetChartOptions( 1, chartShowDates, chartGridMiddle, 0, 0, 0 );
SetBarFillColor( IIf( C > O, ColorRGB( 0, 75, 0 ), IIf( C <= O, ColorRGB( 75, 0, 0 ), colorLightGrey ) ) );
Plot( C, "", IIf( C > O, ColorRGB( 0, 255, 0 ), IIf( C <= O, ColorRGB( 255, 0, 0 ), colorLightGrey ) ), 64, Null, Null, 0, 0, 1 );

//Defaults
ND = Day() != Ref(Day(),-1);
ED = Day()!= Ref(Day(), 1);
TN = TimeNum();
CZ = LastValue(C);
TZ = LastValue(TN);

_SECTION_BEGIN("Options to Options");
DFEED = ParamList("Data Feed", "EST|Amifeed|VBiz|EqualSolutions",0);
EXPDATEN5 = ParamDate( "Expiry Date N50", "12-06-2025", 2 );
EXPDATEBN = ParamDate( "Expiry Date BNF", "26-06-2025", 2 );
EXPDATESX = ParamDate( "Expiry Date SEN", "30-08-2025", 2 );


EnAlgo = ParamToggle("Enable Place Order","No|Yes",1);
EnOrdNo = ParamToggle("Need Order No","No|Yes",0);
LOTS = Param("No. of Lots",1,1,50);
OUTPATH = ParamStr("Path to Save Response","R:\\");
USERNUM = Param("User Number",1,1,100,1);//For Multi User Accounts
HX = Param("Button Horizontal Position",0,0,164);
HY = Param("Button Vertical Position",0,0,89);
_SECTION_END();


//Prefix for Static Variable
STGTAG = "O2O";
SName = STGTAG+Name()+Interval();

Symbol = Name();

if(StrFind( Symbol, "BANKNIFTY" ))
{
SPNAME = "BANKNIFTY";
LOTSIZE = 30;
EXCHANGE = "NFO";
EXPDATE = EXPDATEBN;
}
else if (StrFind( Symbol, "NIFTY" ))
{
SPNAME = "NIFTY";
LOTSIZE = 75;
EXCHANGE = "NFO";
EXPDATE = EXPDATEN5;
}
else if (StrFind( Symbol, "SENSEX" ))
{
SPNAME = "SENSEX";
LOTSIZE = 20;
EXCHANGE = "BFO";
EXPDATE = EXPDATESX;
}
else
{
SPNAME = Symbol;
LOTSIZE = 10;
EXCHANGE = "NSE";
EXPDATE = EXPDATEN5;
}

CEPEFUL = WriteIf(StrFind(Name(),"PE"),"PE","CE");

STRIKE = StrReplace(StrReplace(Symbol,SPNAME,""),CEPEFUL,"");
STRIKE = SYMEXTR(DFEED,STRIKE,EXPDATE);


CEPE = WriteIf(StrFind(Name(),"PE"),"P","C");
EXPDATE = DateTimeFormat("%d",EXPDATE)+StrToUpper( DateTimeFormat("%b",EXPDATE))+DateTimeFormat("%y",EXPDATE);
OPNAME = SPNAME+EXPDATE+CEPE+STRIKE;


//Button Code Start----------------------------------------------------
PX = 10;//round(Status("pxwidth")/164);
PY = 10;//round(Status("pxheight")/89);

Reset = 0;

BuyBut = 2;
SellBut = 3;

ResetBut = 6;


GuiButton( "Buy "+CEPE+"E", BuyBut,  PX*1+PX*HX, PY*2+PY*HY, PY*6, PY*3, notifyClicked );
GuiButton( "Sell "+CEPE+"E", SellBut,  PX*8+PX*HX, PY*2+PY*HY, PY*6, PY*3, notifyClicked );

GuiButton( "Reset", ResetBut, PX*15+PX*HX, PY*2+PY*HY, PY*6, PY*3, notifyClicked );

GuiSetColors( 2, 2, 2, colorBlack, colorLime,colorGreen,-1,colorGreen);
GuiSetColors( 3, 3, 2, colorYellow, colorOrange,colorRed,-1,colorRed);
GuiSetColors( 6, 6, 2, colorBlack, colorYellow, colorGold,-1,colorGold );

function HandleEvents() 
{ 
    for ( n = 0;id = GuiGetEvent( n, 0 );n++ )
    { 
        code = GuiGetEvent( n, 1 ); 

         switch ( id ) 
        { 
             case BuyBut:                                   
             Buy = 1;             
             break; 
             
             case SellBut: 
             Sell = 1;
             break;              
             
             case ResetBut: 
             Reset = 1;
             break;                                      
        } 
    } 
} 
HandleEvents();

if(Reset)
{
StaticVarSet(SName+"INUPTRADE",0);
StaticVarSet(SName+"UPENTRY",0);
StaticVarSet(SName+"UPExitTime",0);

}

//Button Code End--------------------------------------

//Order Place Code Start-----------------------------------------------------------
function FlatTradeOrder(TRANTYPE,TSYM,QTY,PRC,TRGPRC,PRCTYP,PRD,EXCH,UID,TOKEN,REMARKS,OUT)
{
ShellExecute("Curl.exe","https://piconnect.flattrade.in/PiConnectTP/PlaceOrder -d \"jData={\\\"ordersource\\\": \\\"API\\\", \\\"uid\\\": \\\""+UID+"\\\", \\\"actid\\\": \\\""+UID+"\\\", \\\"trantype\\\": \\\""+TRANTYPE+"\\\", \\\"prd\\\": \\\""+PRD+"\\\", \\\"exch\\\": \\\""+EXCH+"\\\", \\\"tsym\\\": \\\""+TSYM+"\\\", \\\"qty\\\": \\\""+QTY+"\\\", \\\"dscqty\\\": \\\"0\\\", \\\"prctyp\\\": \\\""+PRCTYP+"\\\", \\\"prc\\\": \\\""+PRC+"\\\", \\\"trgprc\\\": \\\""+TRGPRC+"\\\", \\\"ret\\\": \\\"DAY\\\", \\\"remarks\\\": \\\""+REMARKS+"\\\", \\\"amo\\\": \\\"NO\\\"}\" -d \"jKey="+TOKEN+"\" "+OUT+"","",0);
return ;
}

/*
//Telegram Alert Function
function Telegram(Key,ID,Message)
{
ih = InternetOpenURL("https://api.telegram.org/bot"+Key+"/sendMessage?chat_id="+ID+"&text="+Message); 
if(ih) InternetClose( ih );
return ;
}

//Telegram
BOTKEY = ParamStr("Telegram Bot Key","");
CHANNELID = ParamStr("Channel ID","");

*/




FLAUID1 = StaticVarGetText("FLAUSERID"+USERNUM);
FLATOK1 = StaticVarGetText("FLATOKEN"+USERNUM);
OUTRESP1 = WriteIf(EnOrdNo,"-o "+OUTPATH+"\\"+Name()+USERNUM+".txt","");
SESSIONOK = FLAUID1 != "" AND FLATOK1 != "" ;

//Quantity
QTY = ""+LOTS * LOTSIZE;

//EXCHANGE = "NFO" ;//NSE NFO CDS MCX BSE BFO

//Messages
BuyMessage = "Buy "+OPNAME+" Qty "+QTY+" @ "+CZ;
SellMessage = "Sell "+OPNAME+" Qty "+QTY+" @ "+CZ;

//Order Place Function Parameters: Buy/Sell, Symbol, Quantity, Price,Trigger Price, PriceType LMT/MKT/SL-LMT/SL-MKT, Product C-CNC M-NRML  H-CO  B-BO I-MIS  F-MTF, Exchange, User ID, Access Token,Respone enable and path

if (LastValue(Buy)  AND Nz(StaticVarGet(SName+"INUPTRADE")) == 0  AND Nz(StaticVarGet(SName+"UPExitTime")) != TZ)
{
if(EnAlgo AND SESSIONOK) FlatTradeOrder("B",OPNAME,QTY,"0","None","MKT","M",EXCHANGE,FLAUID1,FLATOK1,STGTAG,OUTRESP1);
_TRACE(BuyMessage);
StaticVarSet(SName+"INUPTRADE",1);
//PopupWindow(BuyMessage, Name(),5,800,50);
//Telegram(BOTKEY,CHANNELID,BuyMessage); 
}

if (LastValue(Sell)  AND StaticVarGet(SName+"INUPTRADE") )
{
if(EnAlgo AND SESSIONOK) FlatTradeOrder("S",OPNAME,QTY,"0","None","MKT","M",EXCHANGE,FLAUID1,FLATOK1,STGTAG,OUTRESP1);

_TRACE(SellMessage);
StaticVarSet(SName+"INUPTRADE",0);
StaticVarSet(SName+"UPExitTime", TZ);
//PopupWindow(SellMessage, Name(),5,800,50);
//Telegram(BOTKEY,CHANNELID,SellMessage); 
}


//Dashboard

BoardColor = IIf(StaticVarGet(SName+"INUPTRADE") AND CEPE == "C", colorPaleGreen,IIf(StaticVarGet(SName+"INUPTRADE") AND CEPE == "P", colorPink,IIf(EnAlgo  AND SESSIONOK,colorYellow,colorWhite)));
GfxSetBkColor(BoardColor);
GfxSelectSolidBrush(BoardColor);
GfxRoundRect(PX*1, PY*6, PX*28, PY*12,PX,PX);
GfxTextOut(OPNAME+" "+QTY, PX*2, PY*7);
if(EnAlgo AND !SESSIONOK)GfxTextOut("Not Logged In", PX*2, PY*9); 
