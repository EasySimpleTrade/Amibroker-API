/*
VALLAKKOTTAI MURUGAN THUNAI
Easy Simple Trading Solutions
Telegram @easysimpletradeupdates
Website https://easysimpletrade.blogspot.com
Youtube https://www.youtube.com/@easysimpletrade
GitHub https://github.com/EasySimpleTrade
*/

RequestTimedRefresh(1,False);

//Chart Code Start ----------------------------
_N(Title = StrFormat("{{NAME}} - {{INTERVAL}} {{DATE}} O %g, H %g, L %g, C %g  {{VALUES}}", O, H, L, C  ));
SetChartOptions(0,chartShowArrows|chartShowDates);
SetBarFillColor( IIf( C > O, ColorRGB( 0, 75, 0 ), IIf( C <= O, ColorRGB( 75, 0, 0 ), colorLightGrey ) ) );
Plot( C, "", IIf( C > O, ColorRGB( 0, 255, 0 ), IIf( C <= O, ColorRGB( 255, 0, 0 ), colorLightGrey ) ), 64, Null, Null, 0, 0, 1 );
//Chart Code End ---------------------------------------

//Strategy Code Start---------------------------------
Buy = Sell = Short = Cover = 0;
//Strategy Code End -----------------------------------------

//Defaults
CZ = LastValue(C);
TN = TimeNum();
TZ = LastValue(TN);

//Parameters
_SECTION_BEGIN("Kotak Equity Order");
TDVAL = Param("Position Size", 10000,1000,1000000,1000);
EnAlgo = ParamToggle("Enable Algo","No|Yes",1);
EnOrdNo = ParamToggle("Need Order No","No|Yes",0);
OUTPATH = ParamStr("Path to Save Response","R:\\");
EQSUF = ParamStr("NSE Equity Symbol Suffix", "-EQ");
STGTAG = ParamStr("Strategy Tag","EQUITY");

HX = Param("Button Horizontal Position",0,0,164);
HY = Param("Button Vertical Position",0,0,89);
_SECTION_END();

//Prefix for Static Variable
SName = STGTAG+Name()+Interval();

//Button-------------------------------------------

PX = 10;//round(Status("pxwidth")/164);
PY = 10;//round(Status("pxheight")/89);

Reset = 0;

GuiButton( "Buy", 1,  PX*1+PX*HX, PY*2+PY*HY, PY*6, PY*3, notifyClicked );
GuiButton( "Sell", 2,  PX*8+PX*HX, PY*2+PY*HY, PY*6, PY*3, notifyClicked );

GuiButton( "Short",3,  PX*1+PX*HX, PY*6+PY*HY, PY*6, PY*3, notifyClicked );
GuiButton( "Cover", 4,  PX*8+PX*HX, PY*6+PY*HY, PY*6, PY*3, notifyClicked );

GuiButton( "Reset", 5, PX*15+PX*HX, PY*2+PY*HY, PY*6, PY*3, notifyClicked );

GuiSetColors( 1, 2, 2, colorBlack, colorLime,colorGreen,-1,colorGreen);
GuiSetColors( 3, 4, 2, colorYellow, colorOrange,colorRed,-1,colorRed);
GuiSetColors( 5, 6, 2, colorBlack, colorYellow, colorGold,-1,colorGold); 

function HandleEvents() 
{ 
    for ( n = 0;id = GuiGetEvent( n, 0 );n++ )
    { 
        code = GuiGetEvent( n, 1 ); 
         switch ( id ) 
        { 
             case 1:                      
             Buy = 1;             
             break;
              
             case 2:                     
             Sell = 1;             
             break; 
                         
             case 3:                        
             Short= 1;             
             break;
             
             case 4:                       
             Cover = 1;             
             break;
             
             case 5:                      
             Reset = 1;            
             break;   
             
             case 6:                        
             Button6 = 1;            
             break;                           
        } 
    } 
} 
HandleEvents();

//Button Code End--------------------------------------

//API------------------------------------------------------------
NEO_ORD_URL = "https://gw-napi.kotaksecurities.com/Orders/2.0/quick/order/rule/ms/place?sId=";
NEO_KEY = "neotradeapi";

function KotakOrder(TRA_TYP,TRD_SYM,QTY,PRIC,TRG_PRI,PR_TYP,PRODUCT,EXC,SER_ID,SES_ID,ACS_TOK,SES_TOK,OUT)
{
ShellExecute("Curl.exe","-X POST \""+NEO_ORD_URL+""+SER_ID+"\" -H \"accept: application/json\" -H \"Auth: "+SES_TOK+"\" -H \"Sid: "+SES_ID+"\" -H \"neo-fin-key: "+NEO_KEY+"\"  -H \"Content-Type: application/x-www-form-urlencoded\" -H \"Authorization: Bearer "+ACS_TOK+"\" -d \"jData={\\\"am\\\":\\\"NO\\\",\\\"dq\\\":\\\"0\\\",\\\"es\\\":\\\""+EXC+"\\\",\\\"mp\\\":\\\"0\\\",\\\"pc\\\":\\\""+PRODUCT+"\\\",\\\"pf\\\":\\\"N\\\",\\\"pr\\\":\\\""+PRIC+"\\\",\\\"pt\\\":\\\""+PR_TYP+"\\\",\\\"qt\\\":\\\""+QTY+"\\\",\\\"rt\\\":\\\"DAY\\\",\\\"tp\\\":\\\""+TRG_PRI+"\\\",\\\"ts\\\":\\\""+TRD_SYM+"\\\",\\\"tt\\\":\\\""+TRA_TYP+"\\\"}\"  "+OUT+"","",0);
return;
}

/*
//Telegram Alert Function
function Telegram(Key,ID,Message)
{
ih = InternetOpenURL("https://api.telegram.org/bot"+Key+"/sendMessage?chat_id="+ID+"&text="+Message); 
if(ih) InternetClose( ih );
return ;
}

//Telegram
BOTKEY = ParamStr("Telegram Bot Key","");
CHANNELID = ParamStr("Channel ID","");

*/


NEOACSTOK1 = StaticVarGetText("NEOACSTOK"+"1");//"1" - User Number
NEOSESTOK1 = StaticVarGetText("NEOSESTOK"+"1");//"1" - User Number
NEOSESID1 = StaticVarGetText("NEOSESID"+"1");//"1" - User Number
NEOSERID1 = StaticVarGetText("NEOSERVERID"+"1");//"1" - User Number

NEOOUTRESP1 = WriteIf(EnOrdNo,"-o "+OUTPATH+"\\"+Name()+"1"+".txt","");//"1" - User Number
NEOSESSIONOK1 = NEOACSTOK1 != "" AND NEOSESTOK1 != "" ;


EQNAME = StrReplace(Name(),"&","%26")+EQSUF;

//Entry Exit Calculations------------------------------------------
NDO = TimeFrameGetPrice("O",inDaily);
QTY = ""+LastValue(round(TDVAL/NDO));



//Messages
BuyMessage = Name()+" Buy "+C;
SellMessage = Name()+" Sell "+C;
ShortMessage = Name()+" Short "+C;
CoverMessage = Name()+" Cover "+C;

EXCSEG = "nse_cm";//nse_cm, bse_cm, nse_fo, bse_fo, cde_fo, mcx_fo


//Order Place Function Parameters: Buy/Sell, Symbol, Quantity, Price,Trigger Price, PriceType MKT, L, SL, SL-M, Product NRML,CNC,MIS,CO, EXCSEG nse_cm, bse_cm, nse_fo, bse_fo, cde_fo, mcx_fo,API Credentials,Respone enable and path

//Place Order----------------------------------------------------

//Buy
if (LastValue(Buy) AND Nz(StaticVarGet(SName+"INUPTRADE")) == 0 AND Nz(StaticVarGet(SName+"UPExitTime")) != TZ)
{
if(EnAlgo  AND NEOSESSIONOK1) KotakOrder("B",EQNAME,QTY,"0","0","MKT","MIS",EXCSEG,NEOSERID1,NEOSESID1,NEOACSTOK1,NEOSESTOK1,NEOOUTRESP1);
StaticVarSet(SName+"INUPTRADE",1);
StaticVarSet(SName+"UPENTRY",CZ);
_TRACE(BuyMessage);
//PopupWindow(BuyMessage, Name(),5,800,50);
//Telegram(BOTKEY,CHANNELID,BuyMessage); 
}

UPENTRY = StaticVarGet(SName+"UPENTRY");
UPPNL = CZ - UPENTRY;

//Sell
if (LastValue(Sell)  AND StaticVarGet(SName+"INUPTRADE"))
{
if(EnAlgo AND NEOSESSIONOK1) KotakOrder("S",EQNAME,QTY,"0","0","MKT","MIS",EXCSEG,NEOSERID1,NEOSESID1,NEOACSTOK1,NEOSESTOK1,NEOOUTRESP1);
StaticVarSet(SName+"INUPTRADE",0);
StaticVarSet(SName+"UNENTRY",0);
StaticVarSet(SName+"UPExitTime",TZ);
_TRACE(SellMessage);
//PopupWindow(SellMessage, Name(),5,800,50);
//Telegram(BOTKEY,CHANNELID,SellMessage); 
}

//Short
if (LastValue(Short) AND Nz(StaticVarGet(SName+"INDNTRADE")) == 0  AND Nz(StaticVarGet(SName+"DNExitTime")) != TZ)
{
if(EnAlgo  AND NEOSESSIONOK1) KotakOrder("S",EQNAME,QTY,"0","0","MKT","MIS",EXCSEG,NEOSERID1,NEOSESID1,NEOACSTOK1,NEOSESTOK1,NEOOUTRESP1);
StaticVarSet(SName+"INDNTRADE",1);
StaticVarSet(SName+"DNENTRY",CZ);
_TRACE(ShortMessage);
//PopupWindow(ShortMessage, Name(),10,300,50);
//Telegram(BOTKEY,CHANNELID,ShortMessage); 
}

DNENTRY = StaticVarGet(SName+"DNENTRY");
DNPNL = CZ - DNENTRY;

//Cover
if (LastValue(Cover)  AND StaticVarGet(SName+"INDNTRADE"))
{
if(EnAlgo  AND NEOSESSIONOK1) KotakOrder("B",EQNAME,QTY,"0","0","MKT","MIS",EXCSEG,NEOSERID1,NEOSESID1,NEOACSTOK1,NEOSESTOK1,NEOOUTRESP1);
StaticVarSet(SName+"INDNTRADE",0);
StaticVarSet(SName+"DNENTRY",0);
StaticVarSet(SName+"DNExitTime",TZ);
_TRACE(CoverMessage);
//PopupWindow(CoverMessage, Name(),10,700,50);
//Telegram(BOTKEY,CHANNELID,CoverMessage);     
}

//Reset
if(Reset)
{
StaticVarSet(SName+"INUPTRADE",0);
StaticVarSet(SName+"INDNTRADE",0);
StaticVarSet(SName+"UPENTRY",0);
StaticVarSet(SName+"DNENTRY",0);
StaticVarSet(SName+"UPExitTime", 0);
StaticVarSet(SName+"DNExitTime", 0);
}


//Dashboard-----------------------------------------------------
BoardColor = IIf(StaticVarGet(SName+"INUPTRADE"),colorPaleGreen, IIf(StaticVarGet(SName+"INDNTRADE"),colorPink, IIf(EnAlgo  AND NEOSESSIONOK1, colorYellow, colorWhite)));
GfxSetBkColor(BoardColor);
GfxSelectSolidBrush(BoardColor);
GfxRoundRect(PX*1, PY*10, PX*25, PY*18,PX,PX);

If(StaticVarGet(SName+"INUPTRADE"))
{
GfxTextOut(Name()+" Qty "+QTY+" TO "+TDVAL, PX*2, PY*11);
GfxTextOut("Entry "+UPENTRY+" PNL "+UPPNL,PX*2, PY*13);

}
else If(StaticVarGet(SName+"INDNTRADE"))
{
GfxTextOut(Name()+" Qty "+QTY+" TO "+TDVAL, PX*2, PY*11);
GfxTextOut("Entry "+DNENTRY+" PNL "+DNPNL,PX*2, PY*13);
}
else
{
GfxTextOut(Name()+" Qty "+QTY+" TO "+TDVAL, PX*2, PY*11);
}

if(EnAlgo AND !NEOSESSIONOK1)GfxTextOut("Not Logged In", PX*2, PY*15); 
