/*
VALLAKKOTTAI MURUGAN THUNAI
Easy Simple Trading Solutions
Telegram @easysimpletradeupdates
Website https://easysimpletrade.blogspot.com
Youtube https://www.youtube.com/@easysimpletrade
GitHub https://github.com/EasySimpleTrade
*/

RequestTimedRefresh(1,False);

//Chart Code Start ----------------------------
_N(Title = StrFormat("{{NAME}} - {{INTERVAL}} {{DATE}} O %g, H %g, L %g, C %g  {{VALUES}}", O, H, L, C  ));
SetChartOptions(0,chartShowArrows|chartShowDates);
SetBarFillColor( IIf( C > O, ColorRGB( 0, 75, 0 ), IIf( C <= O, ColorRGB( 75, 0, 0 ), colorLightGrey ) ) );
Plot( C, "", IIf( C > O, ColorRGB( 0, 255, 0 ), IIf( C <= O, ColorRGB( 255, 0, 0 ), colorLightGrey ) ), 64, Null, Null, 0, 0, 1 );
//Chart Code End ---------------------------------------

//Strategy Code Start---------------------------------
Buy = Sell = Short = Cover = 0;
//Strategy Code End -----------------------------------------

//Defaults
TN = TimeNum();
CZ = LastValue(C);
TZ = LastValue(TN);

_SECTION_BEGIN("Kotak Options Chart to Options Order");
DFEED = ParamList("Data Feed", "EST|Amifeed|VBiz|EqualSolutions",0);
EXPDATEN5 = ParamDate( "Expiry Date N50", "12-06-2025", 2 );
EXPDATESX = ParamDate( "Expiry Date SEN", "27-05-2025", 2 );
EXPDATEME = ParamDate( "Monthly Expiry Date", "26-06-2025", 2 );

ISWKYN5 = ParamToggle( "Weekly|Monthly N50", "Monthly|Weekly",1);
ISWKYSX = ParamToggle( "Weekly|Monthly SEN", "Monthly|Weekly",1);

EnAlgo = ParamToggle("Enable Place Order","No|Yes",1);
EnOrdNo = ParamToggle("Need Order No","No|Yes",0);
LOTS = Param("No. of Lots",1,1,50);
OUTPATH = ParamStr("Path to Save Response","R:\\");
STGTAG = ParamStr("Strategy Tag","O2O");

HX = Param("Button Horizontal Position",0,0,164);
HY = Param("Button Vertical Position",0,0,89);
_SECTION_END();

//Prefix for Static Variable
SName = STGTAG+Name()+Interval();

//Option Symbol Extract
function SYMEXTR(iDatafeed,iName,iDate)
{
	//Amifeed
	if(iDatafeed == "Amifeed")
		{
		result = StrReplace(iName,"WK","");
		}	
	else 	
	//Valvenet VBiz BestRT
	if(iDatafeed == "VBiz")
		{
		iDATEDMY = DateTimeFormat("%d",iDate) + StrToUpper(DateTimeFormat("%b",iDate)) + DateTimeFormat("%Y",iDate);
		result = StrReplace(StrReplace(StrReplace(iName,"_",""),"-",""),iDATEDMY,"");
		}
	else
	//Equalsolutions
	if(iDatafeed == "EqualSolutions")
		{
		iDATEYM = DateTimeFormat("%y",iDate)+StrToUpper( DateTimeFormat("%b",iDate));
		iDATED = DateTimeFormat("%d",iDate);
		result = StrReplace(StrReplace(StrReplace(iName,"_",""),iDATEYM,""),iDATED,"");
		}
	else
		
	//Default
		{
		result = iName;
		}
    return result;    
}


if (StrFind( Name(), "SENSEX" ))
{
SPNAME = "SENSEX";
STRDIF = 100;
EXPDATE = EXPDATESX;
ISWKY = ISWKYSX;
LOTSIZE = 20;
EXCSEG = "bse_fo";
}
else if(StrFind( Name(), "BANKNIFTY" ))
{
SPNAME = "BANKNIFTY";
STRDIF = 100;
EXPDATE = EXPDATEME;
ISWKY = 0;//ISWKYBN;
LOTSIZE = 35;
EXCSEG = "nse_fo";
}
else if (StrFind( Name(), "NIFTY" ))
{
SPNAME = "NIFTY";
STRDIF = 50;
EXPDATE = EXPDATEN5;
ISWKY = ISWKYN5;
LOTSIZE = 75;
EXCSEG = "nse_fo";
}
else
{
SPNAME = Name();
STRDIF = 1;
EXPDATE = EXPDATEME;
ISWKY = 0;
LOTSIZE = 10;
EXCSEG = "nse_fo";
}

CEPEFUL = WriteIf(StrFind(Name(),"PE"),"PE","CE");

STRIKE = StrReplace(StrReplace(Name(),SPNAME,""),CEPEFUL,"");
STRIKE = SYMEXTR(DFEED,STRIKE,EXPDATE);

FTMM = DateTimeFormat("%#m",EXPDATE);
EXPDATEW = DateTimeFormat("%y",EXPDATE)+WriteIf(StrToNum(FTMM) > 10, StrLeft( DateTimeFormat("%b",EXPDATE),1),FTMM)+DateTimeFormat("%d",EXPDATE);
EXPDATEM = DateTimeFormat("%y",EXPDATE)+StrToUpper( DateTimeFormat("%b",EXPDATE));

if(ISWKY) OPNAME = SPNAME+EXPDATEW+STRIKE+CEPEFUL;
else OPNAME = SPNAME+EXPDATEM+STRIKE+CEPEFUL;

//Button Code Start----------------------------------------------------
PX = 10;//round(Status("pxwidth")/164);
PY = 10;//round(Status("pxheight")/89);

Reset = 0;

GuiButton( "Buy "+CEPEFUL, 2,  PX*1+PX*HX, PY*2+PY*HY, PY*6, PY*3, notifyClicked );
GuiButton( "Sell "+CEPEFUL, 3,  PX*8+PX*HX, PY*2+PY*HY, PY*6, PY*3, notifyClicked );

GuiButton( "Reset", 6, PX*15+PX*HX, PY*2+PY*HY, PY*6, PY*3, notifyClicked );

GuiSetColors( 2, 2, 2, colorBlack, colorLime,colorGreen,-1,colorGreen);
GuiSetColors( 3, 3, 2, colorYellow, colorOrange,colorRed,-1,colorRed);
GuiSetColors( 6, 6, 2, colorBlack, colorYellow, colorGold,-1,colorGold );

function HandleEvents() 
{ 
    for ( n = 0;id = GuiGetEvent( n, 0 );n++ )
    { 
        code = GuiGetEvent( n, 1 ); 

         switch ( id ) 
        { 
             case 2:                                   
             Buy = 1;             
             break; 
             
             case 3: 
             Sell = 1;
             break;              
             
             case 6: 
             Reset = 1;
             break;                                      
        } 
    } 
} 
HandleEvents();

if(Reset)
{
StaticVarSet(SName+"INUPTRADE",0);
StaticVarSet(SName+"UPENTRY",0);
StaticVarSet(SName+"UPExitTime",0);

}

//Button Code End--------------------------------------

//Order Place Code Start-----------------------------------------------------------
NEO_ORD_URL = "https://gw-napi.kotaksecurities.com/Orders/2.0/quick/order/rule/ms/place?sId=";
NEO_KEY = "neotradeapi";

function KotakOrder(TRA_TYP,TRD_SYM,QTY,PRIC,TRG_PRI,PR_TYP,PRODUCT,EXC,SER_ID,SES_ID,ACS_TOK,SES_TOK,OUT)
{
ShellExecute("Curl.exe","-X POST \""+NEO_ORD_URL+""+SER_ID+"\" -H \"accept: application/json\" -H \"Auth: "+SES_TOK+"\" -H \"Sid: "+SES_ID+"\" -H \"neo-fin-key: "+NEO_KEY+"\"  -H \"Content-Type: application/x-www-form-urlencoded\" -H \"Authorization: Bearer "+ACS_TOK+"\" -d \"jData={\\\"am\\\":\\\"NO\\\",\\\"dq\\\":\\\"0\\\",\\\"es\\\":\\\""+EXC+"\\\",\\\"mp\\\":\\\"0\\\",\\\"pc\\\":\\\""+PRODUCT+"\\\",\\\"pf\\\":\\\"N\\\",\\\"pr\\\":\\\""+PRIC+"\\\",\\\"pt\\\":\\\""+PR_TYP+"\\\",\\\"qt\\\":\\\""+QTY+"\\\",\\\"rt\\\":\\\"DAY\\\",\\\"tp\\\":\\\""+TRG_PRI+"\\\",\\\"ts\\\":\\\""+TRD_SYM+"\\\",\\\"tt\\\":\\\""+TRA_TYP+"\\\"}\"  "+OUT+"","",0);
return;
}

/*
//Telegram Alert Function
function Telegram(Key,ID,Message)
{
ih = InternetOpenURL("https://api.telegram.org/bot"+Key+"/sendMessage?chat_id="+ID+"&text="+Message); 
if(ih) InternetClose( ih );
return ;
}

//Telegram
BOTKEY = ParamStr("Telegram Bot Key","");
CHANNELID = ParamStr("Channel ID","");

*/

NEOACSTOK1 = StaticVarGetText("NEOACSTOK"+"1");//"1" - User Number
NEOSESTOK1 = StaticVarGetText("NEOSESTOK"+"1");//"1" - User Number
NEOSESID1 = StaticVarGetText("NEOSESID"+"1");//"1" - User Number
NEOSERID1 = StaticVarGetText("NEOSERVERID"+"1");//"1" - User Number

NEOOUTRESP1 = WriteIf(EnOrdNo,"-o "+OUTPATH+"\\"+Name()+"1"+".txt","");//"1" - User Number
NEOSESSIONOK1 = NEOACSTOK1 != "" AND NEOSESTOK1 != "" ;

//Entry Exit Calculations------------------------------------------
QTY = ""+LOTS * LOTSIZE;

//Messages
BuyMessage = "Buy "+OPNAME+" Qty "+QTY+" @ "+CZ;
SellMessage = "Sell "+OPNAME+" Qty "+QTY+" @ "+CZ;

//Order Place Function Parameters: Buy/Sell, Symbol, Quantity, Price,Trigger Price, PriceType MKT, L, SL, SL-M, Product NRML,CNC,MIS,CO, EXCSEG nse_cm, bse_cm, nse_fo, bse_fo, cde_fo, mcx_fo,API Credentials,Respone enable and path

if (LastValue(Buy)  AND Nz(StaticVarGet(SName+"INUPTRADE")) == 0  AND Nz(StaticVarGet(SName+"UPExitTime")) != TZ)
{
if(EnAlgo AND NEOSESSIONOK1) KotakOrder("B",OPNAME,QTY,"0","0","MKT","NRML",EXCSEG,NEOSERID1,NEOSESID1,NEOACSTOK1,NEOSESTOK1,NEOOUTRESP1);
_TRACE(BuyMessage);
StaticVarSet(SName+"INUPTRADE",1);
//PopupWindow(BuyMessage, Name(),5,800,50);
//Telegram(BOTKEY,CHANNELID,BuyMessage); 
}

if (LastValue(Sell)  AND StaticVarGet(SName+"INUPTRADE") )
{
if(EnAlgo AND NEOSESSIONOK1) KotakOrder("S",OPNAME,QTY,"0","0","MKT","NRML",EXCSEG,NEOSERID1,NEOSESID1,NEOACSTOK1,NEOSESTOK1,NEOOUTRESP1);
_TRACE(SellMessage);
StaticVarSet(SName+"INUPTRADE",0);
StaticVarSet(SName+"UPExitTime", TZ);
//PopupWindow(SellMessage, Name(),5,800,50);
//Telegram(BOTKEY,CHANNELID,SellMessage); 
}


//Dashboard

BoardColor = IIf(StaticVarGet(SName+"INUPTRADE") AND CEPEFUL == "CE", colorPaleGreen,IIf(StaticVarGet(SName+"INUPTRADE") AND CEPEFUL == "PE", colorPink,IIf(EnAlgo  AND NEOSESSIONOK1,colorYellow,colorWhite)));
GfxSetBkColor(BoardColor);
GfxSelectSolidBrush(BoardColor);
GfxRoundRect(PX*1, PY*6, PX*28, PY*12,PX,PX);
GfxTextOut(OPNAME+" "+QTY, PX*2, PY*7);
if(EnAlgo AND !NEOSESSIONOK1)GfxTextOut("Not Logged In", PX*2, PY*9); 
